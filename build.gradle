/**
 * gradle build => build project and distribution packages
 * gradle installDist => to create an image of the application in build/install/projectName
 */

plugins {
  id 'java'
  id 'application'
  id 'checkstyle'
  id "org.sonarqube" version "2.5"
  id "de.undercouch.download" version "3.4.3"
}

// Set the class that should be executed as the application entry point
mainClassName = "jijimaku.AppMain"

// Set project version, this will be part of the generated distributions filenames
project.version = '0.95'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

// Checkstyle configuration
checkstyle {
  toolVersion "8.3"
}

repositories {
  jcenter()
  maven { url 'https://jitpack.io' }
  flatDir { dirs 'lib/udpipe-1.2.0' }
}

dependencies {
  compile group: 'commons-io', name: 'commons-io', version: '2.5'
  compile group: 'org.yaml', name: 'snakeyaml', version: '1.18'
  compile group: 'com.ibm.icu', name: 'icu4j', version: '59.1'
  compile group: 'com.carrotsearch', name: 'langid-java', version: '1.0.0'
  compile group: 'com.atilika.kuromoji', name: 'kuromoji-unidic', version: '0.9.0'
  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.9.1'
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.9.1'

  compile name: 'udpipe'

  // No Maven package available, import via jitpack.io
  compile 'com.github.JDaren:subtitleConverter:-SNAPSHOT'
}

task wrapper(type: Wrapper) {
  gradleVersion = '4.5.1'
}

// This task is slowing things done EVEN when disabled(gradle/plugin bug?), for now just comment it
// UNCOMMENT and execute it to download stopwords lists locally
//task downloadStopwords {
//  List languages = [
//    "arabic",
//    "bulgarian",
//    "catalan",
//    "czech",
//    "danish",
//    "dutch",
//    "english",
//    "finnish",
//    "french",
//    "german",
//    "hindi",
//    "hungarian",
//    "indonesian",
//    "italian",
//    "norwegian",
//    "polish",
//    "portuguese",
//    "romanian",
//    "russian",
//    "slovak",
//    "spanish",
//    "swedish",
//    "turkish",
//    "ukrainian",
//    "vietnamese"
//  ]
//
//  languages.each {
//    String language = it
//    download {
//      src 'https://github.com/Alir3z4/stop-words/raw/master/' + language + '.txt'
//      dest 'language-tags/' + language + '/stopword.txt'
//      onlyIfModified true
//    }
//  }
//}


// Configure distribution package ----------------------
// =====================================================

// Add udpipe native binaries to the distribution package
applicationDistribution.from("lib/udpipe-1.2.0") {
  include "bin-*/*.*"
  into "lib/udpipe-1.2.0"
}

// Add config.yaml file  to the distribution package
applicationDistribution.from("config.yaml") { into "" }

// Add language tags files to the distribution package
applicationDistribution.from("language-tags") { into "language-tags" }

// Exclude the Japanese kuromoji parser because we want to provide it as a separate file
// NOTE: it seems applicationDistribution.exclude(...) does not work in gradle 4.5.1
distributions {
  main {
    contents {
      exclude('**/kuromoji-unidic-*')
    }
  }
}

// Disable TAR archive, for now a ZIP archive is enough
tasks.distTar.enabled = false
